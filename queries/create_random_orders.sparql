prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix mosp: <http://dfki.de/examples/stig_mosp#>
prefix ord: <http://dfki.de/examples/stig_mosp/orders#>
prefix st: <http://dfki.de/stigld/>
prefix schema: <http://schema.org/>

## Create a random order with configurable number of different ordered products
## (Adapt LIMIT parameter to set order batch size)
INSERT {
  ?orderId a schema:Order , st:Topos ; schema:orderedItem ?orderedBox .
}
WHERE {
  {SELECT ?orderId ?orderedBox WHERE {
    ?orderedBox a schema:Product .
    {SELECT (UUID() as ?orderId) WHERE {}}
  } ORDER BY RAND() LIMIT 3 }
} ;

## Establish adjacent links and backlinks to and from existing orders
INSERT {
  ?this st:adjacentTo ?that .
  ?that st:adjacentTo ?this .
}
WHERE {
  ?this a schema:Order ; schema:orderedItem ?item .
  ?that a schema:Order ; schema:orderedItem ?item .
  FILTER(?this != ?that)
}
